#pragma kernel CSMain
static const int threadGroupSize = 1024;

struct Boid {
    float3 position;
    float3 direction;
    
    float3 flockDirection;
    float3 flockCenter;
    float3 separationDirection;
    int numFlockmates;
    int isAlive;
};

struct Neighbor {
    uint boidIdx;
    float3 position;
};

RWStructuredBuffer<Boid> boids;
AppendStructuredBuffer<Neighbor> neighbors;
int numBoids;
int maxNeighbors;
float neighborMaxDist;
float desiredDist;

[numthreads(threadGroupSize, 1, 1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
    Boid currBoid = boids[id.x];
    if (currBoid.isAlive == 1){
        for (int indexBoid = 0; indexBoid < numBoids; indexBoid++) {
            if (id.x != indexBoid) {
                Boid boidI = boids[indexBoid];
                float3 offset = boidI.position - currBoid.position;
                float sqrDist = offset.x * offset.x + offset.y * offset.y + offset.z * offset.z;

                //Checks for neighboring boids within a set radius of the current boid
                if (sqrDist < neighborMaxDist * neighborMaxDist) {
                    currBoid.numFlockmates += 1;
                    Neighbor n;
                    n.boidIdx = indexBoid;
                    n.position = boidI.position;
                    neighbors.Append(n);
                    currBoid.flockDirection += boidI.direction;
                    currBoid.flockCenter += boidI.position;

                    if (sqrDist < desiredDist * desiredDist) {
                        currBoid.separationDirection -= offset;
                    }
                }
            }
        }
    }
    boids[id.x] = currBoid;
}